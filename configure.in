dnl
dnl configure.in for OOPar
dnl
dnl Process this file with GNU autoconf(1) to produce a configure script.
dnl
dnl $Id: configure.in,v 1.5 2004-02-20 11:25:31 longhin Exp $
dnl

dnl ----------------------------------------------------------------------------
dnl                              INITIAL CHECKINGS
dnl ----------------------------------------------------------------------------

dnl Checking if we are in the OOPar source tree
AC_INIT(OOP.mk.in)

dnl Requires a recent version of autoconf
AC_PREREQ(2.13)

dnl Sets the default prefix instalation (/usr/local/oopar)
AC_PREFIX_DEFAULT(/usr/local/oopar)

dnl OOP version stuff
OOP_VERSION=2
OOP_REV=0

AC_SUBST(OOP_VERSION)
AC_SUBST(OOP_REV)

dnl Greetings!
OOP_GREETINGS

dnl System variables
AC_CANONICAL_SYSTEM

dnl Elects c++ as the default testing language
AC_LANG_CPLUSPLUS

dnl Initializes automake (must provide aclocal.m4!)
AM_INIT_AUTOMAKE(oopar, $OOP_VERSION.$OOP_REV)
AM_SANITY_CHECK

dnl Tests the existance of a c++ compiler (m4 macro)
AC_PROG_CXX

dnl Checks for other common programs (gcc, ar, ranlib and install)
AC_PROG_CC
OOP_PROG_AR
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl ----------------------------------------------------------------------------
dnl                          ARCHITECTURE SPECIFICS
dnl ----------------------------------------------------------------------------

dnl Determining standard include location and system architecture
case "$target" in
  i*86-*-linux-*)
    ARCH_INCLUDES=
    ;;
  i*86-*-cygwin*)
    ARCH_INCLUDES=
    ;;
  *)
    AC_MSG_ERROR(cannot determine system architecture.)
    ;;
esac

AC_SUBST(ARCH_INCLUDES)

dnl ----------------------------------------------------------------------------
dnl                              COMPILATION MODE
dnl ----------------------------------------------------------------------------

dnl Sets the compilation mode (devel, profiling, opt)
AC_ARG_WITH(compilation-mode,
  [  --with-compilation-mode=MODE
                          'devel' for development (debug)
                          'profiling' for use with gprof
                          'opt' for maximum optimization],
  [comp_mode=$withval],
  [comp_mode=${comp_mode='devel'}])

case "${comp_mode}" in
  devel)
    echo "Development mode chosen. Debug symbols will be added."
    COMPILATION_MODE="Development compilation mode."
    case "$CXX" in
      c++ | g++)
        MODE_OPTIONS="-DDEBUG -Wall -ggdb3 -pipe -fno-default-inline -fno-inline"
        ;;
      *)
        AC_MSG_ERROR(unsupported compiler.)
        ;;
    esac
    ;;
  profiling)
    echo "Profiling mode chosen. Information for gprof will be added."
    COMPILATION_MODE="Profiling compilation mode."
    case "$CXX" in
      c++ | g++)
        MODE_OPTIONS="-DNODEBUG -DNOTDEBUG -Wall -ggdb3 -pg -pipe"
        ;;
      *)
        AC_MSG_ERROR(unsupported compiler.)
        ;;
    esac
    ;;
  opt)
    echo "Optimization mode chosen. Warp 9 Mr. Sulu!"
    COMPILATION_MODE="Optimization compilation mode."
    case "$CXX" in
      c++ | g++)
        dnl Assumed by -O2, but here anyway :)
        dnl   -fthread-jumps -fomit-frame-pointer -fstrength-reduce
        dnl   -fcse-follow-jumps -fcse-skip-blocks -frerun-cse-after-loop
        dnl   -frerun-loop-opt -fgcse -fexpensive-optimizations
        dnl   -fmove-all-movables

	dnl Use -march=`uname -m` with caution!

	dnl For the future:
	dnl -fno-math-errno -funsafe-math-optimizations -fno-trapping-math \
	dnl -mfpmath=sse

        MODE_OPTIONS="-DNODEBUG -DNOTDEBUG -w -s -O2 -march=`uname -m` -fthread-jumps -fomit-frame-pointer -fstrength-reduce -fcse-follow-jumps -fcse-skip-blocks -frerun-cse-after-loop -frerun-loop-opt -fgcse -fexpensive-optimizations -fmove-all-movables -funroll-loops -ffast-math -fregmove -falign-functions -falign-jumps -falign-loops -malign-double -finline-limit=3000 -fshort-enums -fforce-addr -fdefault-inline -foptimize-sibling-calls -pipe"
        ;;
      *)
        AC_MSG_ERROR(unsupported compiler.)
      ;;
    esac
    ;;
  *)
    AC_MSG_ERROR(bad value ${comp_mode} for --with-compilation-mode)
    ;;
esac

AC_SUBST(COMPILATION_MODE)
AC_SUBST(MODE_OPTIONS)

OOP_CXXFLAGS=$MODE_OPTIONS
OOP_MK="OOP.mk"

AC_SUBST(OOP_CXXFLAGS)
AC_SUBST(OOP_MK)

dnl ----------------------------------------------------------------------------
dnl                              ENABLE MPI?
dnl ----------------------------------------------------------------------------

AC_ARG_ENABLE(mpi,
  [  --enable-mpi=no      Enables the use of MPI [default=no]],
  [mpi_enabled=$enableval],
  [mpi_enabled="no"])

case "${mpi_enabled}" in
  yes)
    echo "Enabling MPI code."
    USING_MPI="-DMPI"
	
  ;;
  no)
    echo "MPI not chosen."
    USING_MPI=""
	
  ;;
  *)
    AC_MSG_ERROR(bad value ${mpi_enabled} for --enable-mpi)
  ;;
esac

AM_CONDITIONAL( MPI, test x${mpi_enabled} = xyes )
AM_CONDITIONAL(OOPMPI, test "$mpi_enabled" = yes)
AC_SUBST(USING_MPI)


dnl ----------------------------------------------------------------------------
dnl                               MPI LOCATION
dnl ----------------------------------------------------------------------------

dnl MPI location
AC_ARG_WITH(mpi-dir,
  [  --with-mpi-dir=DIR    Root of MPI instalation [/usr/local/mpi]],
  [mpi_dir=$withval],
  [mpi_dir=${mpi_dir="/usr/local/mpi"}])

case "${mpi_enabled}" in
  yes)
    MPIDIR=$mpi_dir
    MPIINCLUDE="-I$MPIDIR/include"
    MPILIBDIR="-L$MPIDIR/lib"
    MPILIB="-lmpich"
	MPISOURCE="oopmpicomm.cpp oopmpistorage.cpp"
  ;;
  no)
    MPIDIR=""
    MPIINCLUDE=""
    MPILIBDIR=""
    MPILIB=""
	MPISOURCE=""
  ;;
  *)
    AC_MSG_ERROR(bad value ${mpi_enabled} for --enable-mpi)
  ;;
esac

AC_SUBST(MPIDIR)
AC_SUBST(MPIINCLUDE)
AC_SUBST(MPILIBDIR)
AC_SUBST(MPILIB)
AC_SUBST(MPISOURCE)
dnl ----------------------------------------------------------------------------
dnl                              ENABLE Sloan
dnl ----------------------------------------------------------------------------

AC_ARG_ENABLE(sloan,
  [  --enable-sloan          Enables the use of Sloan],
  [sloan_enabled=$enableval],
  [sloan_enabled="no"])

case "${sloan_enabled}" in
  yes)
    echo "Enabling Sloan code."
    USING_SLOAN="-DUSING_SLOAN"
  ;;
  no)
    echo "Sloan not chosen."
    USING_SLOAN=""
  ;;
  *)
    AC_MSG_ERROR(bad value ${sloan_enabled} for --enable-sloan)
  ;;
esac

AC_SUBST(USING_SLOAN)

dnl ----------------------------------------------------------------------------
dnl                                  CLEANING
dnl ----------------------------------------------------------------------------

OOP_DISTCLEANFILES="*~ *.h.ps *.cc.ps *.gcov *.bb *.bbg *.da gmon.out core octave-core wrong *.info *.prof"

OOP_CLEANFILES="*~ core octave-core *.o *.info"

AC_SUBST(OOP_DISTCLEANFILES)
AC_SUBST(OOP_CLEANFILES)

dnl ----------------------------------------------------------------------------
dnl                              CONGRATULATIONS!
dnl ----------------------------------------------------------------------------

AC_OUTPUT([ Makefile OOP.mk OOP_user.mk \
dnl --| OOP subtree |--
	Sources/Makefile \
	lib/Makefile \
dnl --| Projects subtree |--
	Projects/Makefile \
	Projects/Trivial/Makefile \
	Projects/Flux/Makefile \
  	Projects/BiCGSt/Makefile \
	Projects/PhilFlux/Makefile \
])

OOP_BYEBYE

dnl --| OOP |-----------------------------------------------------------------
